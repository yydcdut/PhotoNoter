apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.yydcdut.note"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode Integer.parseInt(project.VERSION_CODE)
        versionName project.VERSION_NAME
        renderscriptTargetApi 19
        renderscriptSupportModeEnabled true
        multiDexEnabled true

        ndk {
            moduleName = "blur"
            ldLibs "log", "z", "m", "jnigraphics"
            abiFilters "armeabi", "armeabi-v7a", "arm64-v8a", "x86", "x86_64", "mips", "mips64"
        }

        buildConfigField "String", "EVERNOTE_CONSUMER_KEY", (project.hasProperty('EVERNOTE_CONSUMER_KEY') ? "\"$EVERNOTE_CONSUMER_KEY\"" : "\"Your consumer key\"")
        buildConfigField "String", "EVERNOTE_CONSUMER_SECRET", (project.hasProperty('EVERNOTE_CONSUMER_SECRET') ? "\"$EVERNOTE_CONSUMER_SECRET\"" : "\"Your consumer secret\"")
        buildConfigField "String", "TENCENT_KEY", (project.hasProperty('TENCENT_KEY') ? "\"$TENCENT_KEY\"" : "\"TENCENT_KEY\"")
        buildConfigField "String", "SPEECH_ID", (project.hasProperty('SPEECH_ID') ? "\"$SPEECH_ID\"" : "\"SPEECH_ID\"")
    }

    tasks.withType(JavaCompile) {
        //指定编译JDK版本
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    lintOptions {
//        preDexLibraries = false
        checkReleaseBuilds false
        abortOnError false
    }

    signingConfigs {
        release {
        }
        debug {
            storeFile file("debug.keystore")
        }
    }

    buildTypes {
        release {
            shrinkResources true
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.cfg'
            buildConfigField "boolean", "LOG_DEBUG", "false"
        }
        debug {
            signingConfig signingConfigs.debug
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.cfg'
            buildConfigField "boolean", "LOG_DEBUG", "true"
        }
    }

    sourceSets.main {
        jniLibs.srcDir 'src/libs'
    }

    productFlavors {
        Store360 {}
//        QQ {}
//        WanDouJia {}
//        Xiaomi {}
//        Flyme {}
//        Market91 {}
//        AnZhuo {}
//        BaiDu {}
//        AnZhi {}
    }

    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }

    sourceSets { main { jni.srcDirs = ['src/main/jni'] } }
}

android.applicationVariants.all {
    variant ->
        dex.doFirst {
            dex ->
                if (dex.additionalParameters == null) {
                    dex.additionalParameters = []
                }
                dex.additionalParameters += '--multi-dex'
                dex.additionalParameters += '--set-max-idx-number=55000'
//                dex.additionalParameters += "--main-dex-list=$projectDir/multidex.keep".toString()
        }
}

ext {
    dagger2Version = '2.0'
    leakcanaryVersion = '1.3'
}
dependencies {
    //    compile fileTree(include: ['*.jar'], dir: 'src/libs')
    compile "com.android.support:appcompat-v7:${rootProject.ext.supportVersion}"
    compile "com.android.support:support-v13:${rootProject.ext.supportVersion}"
    compile "com.nineoldandroids:library:2.4.0"
    compile "com.android.support:support-annotations:${rootProject.ext.supportVersion}"
    compile "com.android.support:recyclerview-v7:${rootProject.ext.supportVersion}"
    compile "com.android.support:cardview-v7:${rootProject.ext.supportVersion}"
    compile "com.android.support:palette-v7:${rootProject.ext.supportVersion}"
    compile "com.android.support:design:${rootProject.ext.supportVersion}"
    compile "com.jakewharton:butterknife:7.0.1"
    compile "com.evernote:android-sdk:2.0.0-RC3"
    compile "com.yydcdut.sdlv:sdlv:0.4.2@aar"
    compile "com.google.dagger:dagger:${dagger2Version}"
    apt "com.google.dagger:dagger-compiler:${dagger2Version}"
    compile "io.reactivex:rxandroid:1.0.1"
    compile "io.reactivex:rxjava:1.0.14"
    compile "de.greenrobot:eventbus:3.0.0-beta1"
    provided "org.glassfish:javax.annotation:10.0-b28"
    compile "com.commit451:PhotoView:1.2.4"
    compile "com.android.support:multidex:1.0.0"
    compile files("src/libs/umeng-analytics-v5.5.3.jar")
    compile files("src/libs/mta-sdk-1.6.2.jar")
    compile files("src/libs/open_sdk_r5043.jar")
    compile files("src/libs/BaiduLBS_Android.jar")
    compile files("src/libs/Msc.jar")
    compile files("src/libs/Sunflower.jar")
    debugCompile "com.squareup.leakcanary:leakcanary-android:${leakcanaryVersion}"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:${leakcanaryVersion}"
    debugCompile "com.github.moduth:blockcanary:1.0.0"
    releaseCompile "com.github.moduth:blockcanary-no-op:1.0.0"
    compile project(":PG_Edit_SDK")
}

